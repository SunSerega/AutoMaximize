


Кроме удаления неиспользуемых экземпляров классов, сборщик мусора так же может произвольно
перемещать используемые объекты, более плотно упаковая их в памяти.

И он прекрасно справляется с тем, чтобы сделать эти перемещения незаметными, в обычных ситуациях.
Но как только речь находит об указателях и неуправляемом коде - начинаются проблемы.
Чтоб избежать их, надо очень хорошо понимать как работает сборщик мусора.

:::spoiler { summary="Страницы в данной папке опираются на результаты следующего теста:" hidden=true }

```
uses System;
uses System.Runtime.InteropServices;

type
  punch_gc_callback = procedure(ptr: pointer);
  
function get_addr(a: array of integer) := '$'+Marshal.UnsafeAddrOfPinnedArrayElement(a,0).ToString('X');

function ptr_adr<T>(var a: T) := new IntPtr(@a);

function copy_arr(a: IntPtr; punch_gc: punch_gc_callback): IntPtr;
external 'Dll1.dll';
function copy_arr(a: ^integer; punch_gc: punch_gc_callback): IntPtr;
external 'Dll1.dll';

function copy_arr(var a: integer; punch_gc: punch_gc_callback): IntPtr;
external 'Dll1.dll';
function copy_arr(var a: byte; punch_gc: punch_gc_callback): IntPtr;
external 'Dll1.dll';

function copy_arr([MarshalAs(UnmanagedType.LPArray)] a: array of integer; punch_gc: punch_gc_callback): IntPtr;
external 'Dll1.dll';

function copy_arr_recall1(var a: integer; punch_gc: punch_gc_callback): IntPtr :=
copy_arr(@a, punch_gc);

function copy_arr_recall2(a: array of integer; punch_gc: punch_gc_callback): IntPtr :=
copy_arr(a, punch_gc);

function copy_arr_recall3(var a: integer; punch_gc: punch_gc_callback): IntPtr :=
copy_arr(a, punch_gc);

function copy_arr_recall4_helper(a: ^integer; punch_gc: punch_gc_callback): IntPtr;
begin
  punch_gc(a);
  Result := copy_arr(a^, ptr->begin end); // второй раз вызывать punch_gc и вывод - ни к чему, всё ломается уже на предыдущей строчке
end;
function copy_arr_recall4(var a: integer; punch_gc: punch_gc_callback): IntPtr :=
copy_arr_recall4_helper(@a, punch_gc);

function copy_arr_recall5(var a: integer; punch_gc: punch_gc_callback): IntPtr :=
copy_arr(PByte(pointer(@a))^, punch_gc);

function get_int(punch_gc: punch_gc_callback; var a: integer): integer;
begin
  punch_gc(@a);
  Result := 4;
end;
function copy_arr_recall6(var a: integer; punch_gc: punch_gc_callback): IntPtr :=
copy_arr(PByte(pointer(IntPtr(pointer(@a))+get_int(punch_gc, a)))^, ptr->begin end);

// У меня это вызывает смещение массива в памяти, но только при первом вызове
// Writeln тоже вызывает этот метод, поэтому придётся обходиться Console.WriteLine
// Вообще это ужастный костыль, но я не знаю ничего лучше
procedure punch_gc := System.Diagnostics.Debug.WriteLine('');

begin
  var a := Arr(1,2,3,4,5,6);
  var b := Arr(1,2,3,4,5);
  Console.WriteLine('begin');
  Console.WriteLine(get_addr(a));
  Console.WriteLine(get_addr(b));
  
  // punch_gc работает только 1 раз, эти строчки только чтобы протестировать, работает ли он у вас вообще
//  punch_gc;
//  Console.WriteLine('after first gc');
//  Console.WriteLine(get_addr(a));
//  Console.WriteLine(get_addr(b));
  
  {$region заголовки вызова copy_arr}
  
  // безопастно
//  var ptr := copy_arr(a,              // передавать как массив безопастно
//  var ptr := copy_arr(a[0],           // передавать элемент массива var-параметром безопастно
//  var ptr := copy_arr(a[1],           // и это касается не только элемента [0]
//  var ptr := copy_arr_recall2(a,      // безопастно, потому что с точки зрения copy_arr_recall2 ситуация та же что "copy_arr(a,"
//  var ptr := copy_arr_recall3(a[0],   // и var-параметры тоже безопастны через промежуточные подпрограммы
//  var ptr := copy_arr_recall5(a[0],   // тут указатели не попадают в готовый .exe, они только чтобы успокоить компилятор, поэтому безопастно
  
  // НЕ безопастно
//  var ptr := copy_arr(Marshal.UnsafeAddrOfPinnedArrayElement(a,0), ptr-> // GC не следит за содержимым IntPtr
//  var ptr := copy_arr(ptr_adr(a[0]),  // и за другими формами указателей тоже
//  var ptr := copy_arr_recall1(a[0],   // проблема не в передаче адреса возвращаемым значением
//  var ptr := copy_arr_recall4(a[0],   // кроме того, проблема вообще не в неуправляемом коде, в управляемом тоже воспроизводится
//  var ptr := copy_arr_recall6(a[0],   // в отличии от recall5 - тут указатели попадают в готовый .exe, поэтому небезопастно
  
  {$endregion заголовки вызова copy_arr}
  
  var ptr := copy_arr(a,
  ptr->
  begin
    Console.WriteLine('before gc');
    Console.WriteLine(get_addr(a));
    Console.WriteLine('$'+IntPtr(ptr).ToString('X'));
    Console.WriteLine(get_addr(b));
    
    // "b" в любом случае перемещается при punch_gc, его ничего не держит. Таким образом оно показывает что punch_gc успешно сработал
    // Но главное тут - если "ptr" и "a" окажутся разным, значит неуправляемый код потерял настоящий адрес "a" при перемещении
    // Тесты приведённые тут показывают так же что GC вообще не перемещает "a" в любом безопасном сценарии. И никогда не меняет "ptr"
    punch_gc;
    Console.WriteLine('after gc');
    Console.WriteLine(get_addr(a));
    Console.WriteLine('$'+IntPtr(ptr).ToString('X'));
    Console.WriteLine(get_addr(b));
  end);
  
  Console.WriteLine('end');
  Console.WriteLine(get_addr(a));
  Console.WriteLine(get_addr(b));
  
//  punch_gc;
//  Console.WriteLine('after last gc');
//  Console.WriteLine(get_addr(a));
//  Console.WriteLine(get_addr(b));
  
  // Показывает эффекты НЕ безопастного вызова
  // Точнее если неуправляемый код потеряет адрес массива,
  // то тут будет мусор (или ошибка доступа, но её я ни разу не получил)
  var res := new byte[20];
  Marshal.Copy(ptr,res,0,20);
  res.Println;
end.
```
`Dll1.dll` должна быть неуправляемой библиотекой, содержащей следующую функцию (это C++):
```
extern "C" __declspec(dllexport) BYTE* copy_arr(int* a, void (*punch_gc)(void*))
{
    BYTE* res = new BYTE[20]; // выделяем 20 байт неуправляемой памяти
    punch_gc(a); // вызываем ту подпрограмму, чей адрес сюда передали
    memcpy(res, a, 20); // копируем 20 байт из "a" в "res"
    return res; // плохо что неуправляемая память не освобождается, но в этом тесте не важно
}
```
Подробнее о параметрах:
1. `a` принимает указатель на `integer`, что в C++ так же может являеться массивом с элементами типа `integer`;

2. `punch_gc` принемает адрес подпрограммы, принемающей `void*` (безтиповый указатель)
и возвращающей `void` (ничего не возвращающей, то есть это процедура);

3. Ну и возвращаемое значение - `BYTE*`. Так же как `a`, вообще указатель, но в данном случае массив.

Пожалуйста, попробуйте поэксперементировать с этим кодом сами. И если найдёте
что то интересное - обязательно напишите в issue. В этом деле много тестов не бывает.

:::

### В кратце:

Вся безопастность зависит только от объявления подпрограммы. Если подпрограмма принимает:

- Массив или `var`-параметр:\
Пока вызов неуправляемой подпрограммы не завершится - сборщик мусора НЕ будет передвигать объект в памяти.

- Указатель в любом виде (типизированный, безтиповый или даже обёрнутый в запись вроде `IntPtr`):\
Передавать адрес содержимого класса НЕ безопастно.

- Любой размерный тип (то есть запись):\
Сборщик мусора никак не управляет записями, при передаче в
подпрограмму их значение копируется. Поэтому это всегда безопастно.


